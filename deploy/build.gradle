import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

final List<String> commonProjects = List.of("core", "v1_8_R3", "v1_9_R1", "v1_9_R2", "v1_10_R1", "v1_11_R1", "v1_12_R1", "v1_21_R2", "v1_21_R3")
final String name = rootProject.name
final String version = rootProject.version
def libTask = tasks.register("libShadowJar", ShadowJar) {
    archiveBaseName = name
    archiveVersion = version
    archiveClassifier = null

    commonProjects.forEach(prj -> { from project(":" + prj).sourceSets.main.output })
}

def pluginTask = tasks.register("pluginShadowJar", ShadowJar) {
    archiveBaseName = name
    archiveVersion = version
    archiveClassifier = null
    archiveAppendix = "plugin"

    commonProjects.forEach(prj -> { from project(":" + prj).sourceSets.main.output })
    from project(":plugin").sourceSets.main.output
}

sourceSets {
    main {
        java { srcDirs = [] }
        resources { srcDirs = [] }
    }
}

tasks.assemble {
    dependsOn libTask, pluginTask
}
